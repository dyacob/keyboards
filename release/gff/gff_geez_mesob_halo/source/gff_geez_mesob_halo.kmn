c =====================Begin Identity Section===================================================
c 
c The Geʾez Frontier Foundation's mnemonic input method for Geʾez (Ethiopic) script on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 14 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_amharic/2.0/gff_amharic.php
c Source        :  https://github.com/keymanapp/keyboards/tree/master/release/gff/gff_amharic
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
store(&VERSION) '10.0'
store(&NAME) 'Geʾez Mesob-Halo'
store(&COPYRIGHT) '© 2022 Geʾez Frontier Foundation'
store(&Message) "This is an Amharic language mnemonic input method for Ethiopic script that requires Unicode 4.1 support."
store(&CapsAlwaysOff) '1'
store(&LAYOUTFILE) 'gff_geez_mesob_halo.keyman-touch-layout'
store(&KMW_EMBEDCSS) 'gff_geez_mesob_halo.css'
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c The remainder of this document is formatted with a fixed width font.

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ)  'ሀለሐ𞟠መሠረሰሸቀቐበቨተቸኀነኘከኸወዐዘዠየደጀገጘጠጨጰጸፀፈፐ'
  store(ካዕብ)  'ሁሉሑ𞟡ሙሡሩሱሹቁቑቡቩቱቹኁኑኙኩኹዉዑዙዡዩዱጁጉጙጡጩጱጹፁፉፑ'
  store(ሣልስ)  'ሂሊሒ𞟢ሚሢሪሲሺቂቒቢቪቲቺኂኒኚኪኺዊዒዚዢዪዲጂጊጚጢጪጲጺፂፊፒ'
  store(ራብዕ)  'ሃላሓ𞟣ማሣራሳሻቃቓባቫታቻኃናኛካኻዋዓዛዣያዳጃጋጛጣጫጳጻፃፋፓ'
  store(ኃምስ)  'ሄሌሔ𞟤ሜሤሬሴሼቄቔቤቬቴቼኄኔኜኬኼዌዔዜዤዬዴጄጌጜጤጬጴጼፄፌፔ'
  store(ሳድስ)  'ህልሕ𞟥ምሥርስሽቅቕብቭትችኅንኝክኽውዐዝዥይድጅግጝጥጭጵጽፅፍፕ'
  store(ሳብዕ)  'ሆሎሖ𞟦ሞሦሮሶሾቆቖቦቮቶቾኆኖኞኮኾዎዖዞዦዮዶጆጎጞጦጮጶጾፆፎፖ'

c QWERTY keymappings
  store(keys)  'hlH_m_rsxqQbvtc_nNkKw_zZydjgGTCPS_fp'

c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)  [T_ሳድስ]'aAuUiIeEoO'[T_ኃምስ]
  store(loneVowelEthio) 'አአአኡኡኢኢእእኦኦኤ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ግዕዝ፡መደበኛ)     'ሀለሐመሠረሰሸቀቐበቨተቸኀነኘከኸዘዠደጀገጘጠጨጰጸፈፐ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ኋሏሗሟሧሯሷሿቋቛቧቯቷቿኋኗኟኳዃዟዧዷጇጓጟጧጯጷጿፏፗ'

c 'hkqQgG'
  store(ዘመደ፡ግዕዝ) '𞟨ᎀᎄኈዀቘⶓᎈᎌ'
  store(ዘመደ፡ካዕብ) '𞟫ᎃᎇኍዅቝⶖᎋᎏ'
  store(ዘመደ፡ሣልስ) '𞟩𞟭𞟳ኊዂቚⶔ𞟻𞟽'
  store(ዘመደ፡ራብዕ) 'ሗሟቧኋዃቛጟፏፗ'
  store(ዘመደ፡ኃምስ) '𞟪𞟮𞟴ኌዄቜⶕ𞟼𞟾'

store(ዘመደ፡ራብዕ፡ግዕዝ) 'ቋኳጓ'
store(ዘመደ፡ግዕዝ፡ግዕዝ) 'ቈኰጐ'
store(ዘመደ፡ሣልስ፡ጉራጌ) '𞟰𞟵𞟸'
store(ዘመደ፡ሣልስ፡ግዕዝ) 'ቊኲጒ'
store(ዘመደ፡ኃምስ፡ግዕዝ) 'ቌኴጔ'
store(ዘመደ፡ኃምስ፡ጉራጌ) '𞟱𞟶𞟹'
store(ዘመደ፡ካዕብ፡ግዕዝ) 'ቍኵጕ'
store(ዘመደ፡ካዕብ፡ጉራጌ) '𞟲𞟷𞟺'
c ---------------------End Labiovelars----------------------------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'mobile tablet'

  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c 
  + any(keys)     > index(ግዕዝ,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  + [T_1220] > 'ሠ'
  + [T_1280] > 'ኀ'
  + [T_12A0] > 'አ'
  + [T_12D0] > 'ዐ'
  + [T_1340] > 'ፀ'
  + [T_1E7E0] > '𞟠'
  any(ግዕዝ) + [T_ካዕብ] > index(ካዕብ,1)
  any(ግዕዝ) + [T_ሣልስ] > index(ሣልስ,1)
  any(ግዕዝ) + [T_ራብዕ] > index(ራብዕ,1)
  any(ግዕዝ) + [T_ኃምስ] > index(ኃምስ,1)
  any(ግዕዝ) + [T_ሳድስ] > index(ግዕዝ,1)
  any(ግዕዝ) + [T_ሳብዕ] > index(ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ግዕዝ፡መደበኛ) + [T_WWA] > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ዘመደ፡ራብዕ)  + [T_WWA] > index(ዘመደ፡ግዕዝ,1)
  any(ዘመደ፡ግዕዝ)  + [T_WWA] > index(ዘመደ፡ሣልስ,1)
  any(ዘመደ፡ሣልስ)  + [T_WWA] > index(ዘመደ፡ኃምስ,1)
  any(ዘመደ፡ኃምስ)  + [T_WWA] > index(ዘመደ፡ካዕብ,1)

  any(ዘመደ፡ራብዕ፡ግዕዝ) + [T_WWA] > index(ዘመደ፡ግዕዝ፡ግዕዝ,1)
  any(ዘመደ፡ግዕዝ፡ግዕዝ) + [T_WWA] > index(ዘመደ፡ሣልስ፡ግዕዝ,1)
  any(ዘመደ፡ሣልስ፡ግዕዝ) + [T_WWA] > index(ዘመደ፡ሣልስ፡ጉራጌ,1)
  any(ዘመደ፡ሣልስ፡ጉራጌ) + [T_WWA] > index(ዘመደ፡ኃምስ፡ግዕዝ,1)
  any(ዘመደ፡ኃምስ፡ግዕዝ) + [T_WWA] > index(ዘመደ፡ኃምስ፡ጉራጌ,1)
  any(ዘመደ፡ኃምስ፡ጉራጌ) + [T_WWA] > index(ዘመደ፡ካዕብ፡ግዕዝ,1)
  any(ዘመደ፡ካዕብ፡ግዕዝ) + [T_WWA] > index(ዘመደ፡ካዕብ፡ጉራጌ,1)
c ---------------------End Input of Labiovelars-------------------------------------------------

  + [T_THOUSAND] > '፲፻'

c =====================End Functional Section===================================================
